PROGRAM_NAME='Tesira_Phone2'

(***********************************************************)
(***********************************************************)
(*  FILE_LAST_MODIFIED_ON: 02/08/2019  AT: 10:08:17        *)
(***********************************************************)
(* System Type : NetLinx                                   *)
(***********************************************************)
(* REV HISTORY:                                            *)
(***********************************************************)
(*
    $History: $
    Notes
	https://support.biamp.com/Tesira/Control/Tesira_command_string_calculator
	
	?? RIBED$0D$0A - Returning Feedback
	
	"'SESSION set verbose true',$0A"
	"'SESSION set detailedResponse true',$0A"
	
	Output 1 = Vadio..
	Output 2 = Ceiling...
	Output 4 = Front Speakers
	
*)
(***********************************************************)
(*          DEVICE NUMBER DEFINITIONS GO BELOW             *)
(***********************************************************)
DEFINE_DEVICE

#IF_NOT_DEFINED dvTP_Biamp
dvTP_Biamp =					10001:5:0
#END_IF

#IF_NOT_DEFINED dvTP_Biamp2
dvTP_Biamp2 =					10002:5:0
#END_IF

#IF_NOT_DEFINED dvBiamp
dvBiamp =						5001:3:0 //Tesira TOP Biamp
#END_IF


(***********************************************************)
(*               CONSTANT DEFINITIONS GO BELOW             *)
(***********************************************************)
DEFINE_CONSTANT

COMPENSATE			= 88
MAX_COMP			= 88
MAX_SPAN				= 6

VOL_UP				= 1
VOL_DN				= -1

ON_HOOK				= 0 //not connected
OFF_HOOK				= 1



TXT_PROGRAM			= 10
TXT_CALLER			= 11
TXT_MYPHONE			= 20

#IF_NOT_DEFINED CR
CR				= 13
#END_IF

#IF_NOT_DEFINED LF
LF				= 10
#END_IF


TL_SET				= 51

YES_ON				= 'true'
YES_OFF				= 'false'

TAG_PRGM				= 'Program'
TAG_CEILING			= 'privacymute'

ID_PROGRAM			= 1
ID_CEILING				= 1

(***********************************************************)
(*               VARIABLE DEFINITIONS GO BELOW             *)
(***********************************************************)
DEFINE_VARIABLE

CHAR nBiampDevice[30] = 'Tesira VT'
CHAR nMyPhone[15] = '404-385-0677'

VOLATILE INTEGER nBiampOnline

VOLATILE LONG lTLBiamp[] = {250}
VOLATILE CHAR nBiampBuffer[2500]

VOLATILE SINTEGER nProgram_Preset = -15 //Set Default Level
VOLATILE SINTEGER nProgram_Level //Caller + Speakers
VOLATILE INTEGER nProgram_Mute

VOLATILE INTEGER nCeiling_Mute //Ceiling Mics

VOLATILE CHAR dialPhone[20] //Dialer1
VOLATILE CHAR sLastNumber[20]
VOLATILE CHAR sIncomingCall[20]
VOLATILE INTEGER nPhoneState

//Biamp Level Ranges...
VOLATILE SINTEGER nMaximum = 12 //Max...
VOLATILE SINTEGER nMinimum = -88 //Minimum...

DEV vdvTP_Biamp[] = {dvTP_Biamp, dvTP_Biamp2}

VOLATILE INTEGER nChnlbtns[] =
{
    //Program...
    37,38,39,40,
    
    //Ceiling Mute
    50 //
}

    
(***********************************************************)
(*        SUBROUTINE/FUNCTION DEFINITIONS GO BELOW         *)
(***********************************************************)
(* EXAMPLE: DEFINE_FUNCTION <RETURN_TYPE> <NAME> (<PARAMETERS>) *)
(* EXAMPLE: DEFINE_CALL '<NAME>' (<PARAMETERS>) *)  
DEFINE_FUNCTION fnMuteCeilingMics(CHAR cTag[], INTEGER cIn,    CHAR cMute[MAX_SPAN])
{
    //Toggle Logic State...
    SEND_STRING dvBiamp, "cTag,' set state  ',ITOA(cIn),' ',cMute,CR" 
}
DEFINE_FUNCTION fnMuteVolume(CHAR cTag[], INTEGER cIn, CHAR cValue[MAX_SPAN]) 
{
    SEND_STRING dvBiamp, "cTag,' set mute ',ITOA(cIn),' ',cValue,CR"
}
DEFINE_FUNCTION fnSetVolumeUp(CHAR cTag[], INTEGER cIn, SINTEGER cVolume)
{
    IF (nProgram_Level < nMaximum )
    {
	SEND_STRING dvBiamp, "cTag,' set level ',ITOA(cIn),' ',ITOA(cVolume + VOL_UP),CR"
    }
}
DEFINE_FUNCTION fnSetVolumeDown(CHAR cTag[], INTEGER cIn, SINTEGER cVolume)
{
    IF (nProgram_Level > nMinimum )
    {
	SEND_STRING dvBiamp, "cTag,' set level ',ITOA(cIn),' ',ITOA(cVolume + VOL_DN),CR"
    }
}
DEFINE_FUNCTION fnSetVolumePreset(CHAR cTag[], INTEGER cIn, SINTEGER cLevel)
{
    SEND_STRING dvBiamp, "cTag,' set level ',ITOA(cIn),' ',ITOA(cLevel),CR"
}
DEFINE_FUNCTION fnGetValues()
{
    IF (nProgram_Mute)
    {
	SEND_COMMAND vdvTP_Biamp, "'^TXT-',ITOA(TXT_PROGRAM),',0,Muted'"
    }
    ELSE
    {
	SEND_COMMAND vdvTP_Biamp, "'^TXT-',ITOA(TXT_PROGRAM),',0,',ITOA(nProgram_Level + MAX_COMP),'%'"
    }    
    
    WAIT 50 SEND_STRING dvBiamp, "'TIControlStatus1 get hookState',CR"
}
DEFINE_FUNCTION fnSubscribeCalls()
{
    SEND_STRING dvBiamp, "'VoIPControlStatus1 subscribe callState Calling',CR"
}
DEFINE_FUNCTION fnParseTesira()
{
	STACK_VAR CHAR cResponse[500] CHAR cTrash[500]
	LOCAL_VAR CHAR cMsg[100]
	LOCAL_VAR CHAR cMsgId[100]

	WHILE(FIND_STRING(nBiampBuffer,"CR,LF",1))
	{	
	    cResponse = REMOVE_STRING(nBiampBuffer,"CR,LF",1)
    
	SELECT
	{
	    //Full Caller ID Parse...
	    ACTIVE(FIND_STRING(cResponse,'"action":UI_DISPLAY_STATUS "cid":"\"',1)):
	    {

		REMOVE_STRING(cResponse,'"action":UI_DISPLAY_STATUS "cid":"\"',1) //Left with --> 04011103\"\"6789929391\"\"Wireless Caller\""$0D$0A
		
		REMOVE_STRING(cResponse,'\"\"',1) //Should be left with --> 6789929391\"\"Wireless Caller\""$0D$0A
	    
		cMsg = LEFT_STRING(cResponse, 10) //Just get 10 digit phone number
		REMOVE_STRING(cResponse, '\"\"',1) //Should be left with --> Wireless Caller\""$0D$0A
		
		//cMsgId = cResponse 
		cMsgId = LEFT_STRING(cResponse, 10)

		//SET_LENGTH_STRING (cMsgId,LENGTH_STRING(cMsgId) -5); //Should be left with --> Wireless Caller$0D$0A
		
		
		SEND_COMMAND vdvTP_Biamp, "'^TXT-19,0,',cMsg" //Place the Number
		SEND_COMMAND vdvTP_Biamp, "'^TXT-18,0,',cMsgId" //Place the Name...
	    }
	    ACTIVE(FIND_STRING(cResponse,'VoIPControlStatus1 dial 1 1 ',1)):
	    {
		REMOVE_STRING(cResponse,'VoIPControlStatus1 dial 1 1 ',1)
		cMsg = cResponse
		
		 SEND_COMMAND vdvTP_Biamp, "'^TXT-19,0,',cMsg" //Phone Number Block...
		 nPhoneState = OFF_HOOK
		 SEND_COMMAND vdvTP_Biamp, "'^TXT-21,0,Connected'"
	    }
	    ACTIVE(FIND_STRING(cResponse,'VoIPControlStatus1 end 1 1 ',1)):
	    {
		 nPhoneState = ON_HOOK
		 SEND_COMMAND vdvTP_Biamp, "'^TXT-21,0,Disconnected'"
	    }
	    ACTIVE(FIND_STRING(cResponse,'"state":VOIP_CALL_STATE_RINGING',1)): //UPDATED!!!!
	    {
		SEND_COMMAND vdvTP_Biamp, "'@PPX'"
		SEND_COMMAND vdvTP_Biamp, "'PPON-IncomingCall'" //Show Caller PopuP
		SEND_COMMAND vdvTP_Biamp, "'^TXT-21,0,Incoming Call'"
	    }
	    ACTIVE(FIND_STRING(cResponse,'"state":VOIP_CALL_STATE_INIT',1)):
	    {
		SEND_COMMAND vdvTP_Biamp, "'^TXT-21,0,Connected'"
		nPhoneState = OFF_HOOK
	    }
	    ACTIVE(FIND_STRING(cResponse,'"prompt":VOIP_PROMPT_CONNECTING',1)):
	    {
		SEND_COMMAND vdvTP_Biamp, "'^TXT-21,0,Connected'"
		nPhoneState = OFF_HOOK
	    }
	    ACTIVE(FIND_STRING(cResponse,'"state":VOIP_CALL_STATE_DIAL',1)):
	    {
		SEND_COMMAND vdvTP_Biamp, "'^TXT-21,0,Connecting'"
		nPhoneState = OFF_HOOK
	    }
	    ACTIVE(FIND_STRING(cResponse,'"state":VOIP_CALL_STATE_ANSWER_CALL',1)):
	    {
		SEND_COMMAND vdvTP_Biamp, "'^TXT-21,0,Connected'"
		nPhoneState = OFF_HOOK
	    }
	    
	    //Ceiling Mutes...
	    ACTIVE(FIND_STRING(cResponse,'privacymute set state 1 true',1)): 
	    {
		ON [nCeiling_Mute]
	    }
	    ACTIVE(FIND_STRING(cResponse,'privacymute set state 1 false',1)):
	    {
		OFF [nCeiling_Mute]
	    }

	    //Program  Mutes and Levels...
	    ACTIVE(FIND_STRING(cResponse,'Program set mute 1 true',1)): //Set
	    {
		ON [nProgram_Mute]
		SEND_COMMAND vdvTP_Biamp, "'^TXT-',ITOA(TXT_PROGRAM),',0,Muted'"
	    }
	    ACTIVE(FIND_STRING(cResponse,'Program set mute 1 false',1)):
	    {
		OFF [nProgram_Mute]
		SEND_COMMAND vdvTP_Biamp, "'^TXT-',ITOA(TXT_PROGRAM),',0,',ITOA(nProgram_Level + MAX_COMP),'%'"
	    }
	    ACTIVE(FIND_STRING(cResponse,'Program set level 1',1)):
	    {
		REMOVE_STRING(cResponse,'Program set level 1',1)
		nProgram_Level = ATOI(cResponse)
		
		SEND_COMMAND vdvTP_Biamp, "'^TXT-',ITOA(TXT_PROGRAM),',0,',ITOA(nProgram_Level + MAX_COMP),'%'"
	    }
	}
    }
}

(***********************************************************)
(*                STARTUP CODE GOES BELOW                  *)
(***********************************************************)
DEFINE_START

ON [nBiampOnline]
CREATE_BUFFER dvBiamp,nBiampBuffer;


WAIT 600 //1 Minute
{
    OFF [nBiampOnline]
}

(***********************************************************)
(*                THE EVENTS GO BELOW                      *)
(***********************************************************)
DEFINE_EVENT
BUTTON_EVENT [vdvTP_Biamp, 1]
BUTTON_EVENT [vdvTP_Biamp, 2]
BUTTON_EVENT [vdvTP_Biamp, 3]
BUTTON_EVENT [vdvTP_Biamp, 4]
BUTTON_EVENT [vdvTP_Biamp, 5]
BUTTON_EVENT [vdvTP_Biamp, 6]
BUTTON_EVENT [vdvTP_Biamp, 7]
BUTTON_EVENT [vdvTP_Biamp, 8]
BUTTON_EVENT [vdvTP_Biamp, 9]
BUTTON_EVENT [vdvTP_Biamp, 10]
BUTTON_EVENT [vdvTP_Biamp, 11] 
BUTTON_EVENT [vdvTP_Biamp, 12] //Dial Pad...
{                                    
    PUSH :
    {
	STACK_VAR INTEGER nPhoneBtns
	STACK_VAR CHAR nDigit[1];
	
	nPhoneBtns = BUTTON.INPUT.CHANNEL - 1;

	TO[BUTTON.INPUT];
	
	SWITCH (nPhoneBtns)
	{
	    CASE 10:
	    {
		nDigit = '*';
		BREAK;
	    }
	    CASE 11:
	    {
		nDigit = '#';
		BREAK;
	    }
	    DEFAULT:
	    {
		nDigit = ITOA (nPhoneBtns);
		BREAK;
	    }
	}
	dialPhone = "dialPhone, nDigit"
	
	IF ( nPhoneState == OFF_HOOK )
	{
	    SEND_STRING dvBiamp, "'VoIPControlStatus1 dtmf  1 ',nDigit,CR"
	}
	
	SEND_COMMAND vdvTP_Biamp, "'^TXT-17,0,',dialPhone";
	
    }
}
BUTTON_EVENT [vdvTP_Biamp, 13] //BackSpace...
{
    PUSH :
    {
	IF (LENGTH_STRING(dialPhone))
	{
	    TO[BUTTON.INPUT];
	    SET_LENGTH_STRING(dialPhone,LENGTH_STRING(dialPhone) -1);
	    SEND_COMMAND vdvTP_Biamp, "'^TXT-17,0,',dialPhone";
	}
    }
}
BUTTON_EVENT [vdvTP_Biamp, 14] //Clear...
{
    PUSH :
    {
	dialPhone = "";
	SEND_COMMAND vdvTP_Biamp, "'^TXT-17,0,',dialPhone";
    }
}
BUTTON_EVENT [vdvTP_Biamp, 15] //Dial...
{
    PUSH :
    {
	IF (LENGTH_STRING (dialPhone) > 0)
	{
	    TO[BUTTON.INPUT]
	    SEND_STRING dvBiamp, "'VoIPControlStatus1 dial 1 1 ',dialPhone,CR"
	    nPhoneState = OFF_HOOK
		SEND_COMMAND vdvTP_Biamp, "'^TXT-21,0,Connected'"
	}
    }
}
BUTTON_EVENT [vdvTP_Biamp, 16] //Redial...
{
    PUSH :
    {
	TO[BUTTON.INPUT]
	SEND_STRING dvBiamp, "'VoIPControlStatus1 redial 1 1',CR"
    }
}
BUTTON_EVENT [vdvTP_Biamp, 17] //HangUp...
{
    PUSH :
    {
	TO[BUTTON.INPUT]
	SEND_STRING dvBiamp, "'VoIPControlStatus1 end 1 1 ',CR"
	
	nPhoneState = ON_HOOK
	SEND_COMMAND vdvTP_Biamp, "'^TXT-21,0,Disconnected'"
    }
}
BUTTON_EVENT [vdvTP_Biamp, 18] //Answer...
{
    PUSH :
    {
	TO[BUTTON.INPUT]
	SEND_STRING dvBiamp, "'VoIPControlStatus1 answer 1 1 ',CR"
	SEND_COMMAND vdvTP_Biamp, "'PPOF-IncomingCall'" //Remove Me...
	WAIT 10
	{
	    //SEND_STRING dvBiamp, "'TIControlStatus1 get hookState',$0A" 
	    SEND_COMMAND vdvTP_Biamp, "'PPON-_Phone'"
	    	    
	}
    }
}
BUTTON_EVENT [vdvTP_Biamp, 19] //Ignore Call
{
    PUSH :
    {
	TO[BUTTON.INPUT]
	SEND_COMMAND vdvTP_Biamp, "'@PPK-IncomingCall'"
	SEND_STRING dvBiamp, "'VoIPControlStatus1 answer 1 1 ',CR"
	WAIT 10
	{
	   SEND_STRING dvBiamp, "'VoIPControlStatus1 end 1 1 ',CR"
	}
    }
}
BUTTON_EVENT [vdvTP_Biamp, nChnlbtns]
{
    PUSH :
    {
	STACK_VAR INTEGER nChnlIdx
	
	nChnlIdx = GET_LAST (nChnlbtns)
	SWITCH (nChnlIdx)
	{
	    

	    //Program
	    CASE 1 :
	    {
		IF (!nProgram_Mute)
		{
		    fnMuteVolume(TAG_PRGM, ID_PROGRAM, YES_ON)
		}
		ELSE
		{
		    fnMuteVolume(TAG_PRGM, ID_PROGRAM, YES_OFF)
		}
	    }
	    CASE 2 : fnSetVolumeUp(TAG_PRGM, ID_PROGRAM, nProgram_Level)
	    CASE 3 : fnSetVolumeDown(TAG_PRGM, ID_PROGRAM, nProgram_Level)
	    CASE 4 : fnSetVolumePreset(TAG_PRGM, ID_PROGRAM, nProgram_Preset)
	            
	    //Ceiling
	    CASE 5 : 
	    {
		IF(!nCeiling_Mute)
		{
		    fnMuteCeilingMics(TAG_CEILING, ID_CEILING, YES_ON)
		}
		ELSE
		{
		    fnMuteCeilingMics(TAG_CEILING, ID_CEILING, YES_OFF)
		}
	    }
	}
    }
    HOLD [2, REPEAT] :
    {
	STACK_VAR INTEGER nChnlIdx
	
	nChnlIdx = GET_LAST (nChnlbtns)
	SWITCH (nChnlIdx)
	{
	    CASE 2 : fnSetVolumeUp(TAG_PRGM, ID_PROGRAM, nProgram_Level)
	    CASE 3 : fnSetVolumeDown(TAG_PRGM, ID_PROGRAM, nProgram_Level)
	    
	}
    }
}

DEFINE_EVENT
DATA_EVENT [dvTP_Biamp]
{
    ONLINE:
    {
	SEND_COMMAND vdvTP_Biamp, "'^TXT-100,0,',nBiampDevice"
	SEND_COMMAND vdvTP_Biamp, "'^TXT-',ITOA(TXT_MYPHONE),',0,',nMyPhone"
	
	IF (!nBiampOnline)
	{
	    fnGetValues()
	}
    }
}
DATA_EVENT [dvBiamp]
{
    ONLINE:
    {
	    SEND_COMMAND DATA.DEVICE, "'SET BAUD 115200,N,8,1 485 DISABLED'"
	    SEND_COMMAND DATA.DEVICE, "'RXON'"
	    SEND_COMMAND DATA.DEVICE, "'HSOFF'"
	    WAIT 100
	    {
		    fnSetVolumePreset(TAG_PRGM, ID_PROGRAM, nProgram_Preset)
		    WAIT 50
		    {
			fnSubscribeCalls()
		    }
	    }
    }
    STRING:
    {
	fnParseTesira()
    }
}


DEFINE_EVENT

TIMELINE_EVENT[TL_FEEDBACK]
{

    [vdvTP_Biamp, 37] = nProgram_Mute
    [vdvTP_Biamp, 50] = nCeiling_Mute

}


