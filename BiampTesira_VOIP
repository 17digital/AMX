PROGRAM_NAME='Tesira_Phone2'

(***********************************************************)
(***********************************************************)
(*  FILE_LAST_MODIFIED_ON: 08/21/2019  AT: 07:07:15        *)
(***********************************************************)
(* System Type : NetLinx                                   *)
(***********************************************************)
(* REV HISTORY:                                            *)
(***********************************************************)
(*
    $History: $
    Notes
	https://support.biamp.com/Tesira/Control/Tesira_command_string_calculator
	
	?? RIBED$0D$0A - Returning Feedback
	
	"'DEVICE get ipStatus control',13"
	
	Output 1 = Vadio..
	Output 2 = Ceiling...
	Output 4 = Front Speakers
	
*)
(***********************************************************)
(*          DEVICE NUMBER DEFINITIONS GO BELOW             *)
(***********************************************************)
DEFINE_DEVICE

#IF_NOT_DEFINED dvTP_Biamp
dvTP_Biamp =					10001:5:0
#END_IF

#IF_NOT_DEFINED dvTP_Biamp2
dvTP_Biamp2 =					10002:5:0
#END_IF

#IF_NOT_DEFINED dvBiamp
dvBiamp =						5001:7:0 //Tesira TOP Biamp
#END_IF


(***********************************************************)
(*               CONSTANT DEFINITIONS GO BELOW             *)
(***********************************************************)
DEFINE_CONSTANT

#IF_NOT_DEFINED CR
CR				= 13
#END_IF

#IF_NOT_DEFINED LF
LF				= 10
#END_IF


MAX_COMP				= 88
MAX_SPAN				= 6

VOL_UP				= 1
VOL_DN				= -1

ID_PROGRAM			= 1
ID_CEILING				= 1
ID_LAV_1				= 1
ID_LAV_2				= 2
ID_LAV_3				= 3
ID_LAV_4				= 4

TXT_LAV_1				= 1
TXT_LAV_2				= 2
TXT_LAV_3				= 3
TXT_LAV_4				= 4
TXT_PROGRAM	 		= 9

TXT_MYPHONE			= 20
TXT_SIPDISAPLAY_NAME		= 22
TXT_CID_NUMBER			= 19
TXT_CID_NAME			= 18
TXT_PHONE_STATE			= 21
TXT_NUMBER_DISPLAY			= 17

YES_ON				= 'true'
YES_OFF				= 'false'

TAG_PRGM				= 'Program'
TAG_CEILING			= 'privacymute'
TAG_MIC_MUTE		= 'MicMutes'
TAG_MIC_MIX			= 'MixOuts'

//Buttons...
BTN_MUTE_LAV1			= 21
BTN_MUTE_LAV2			= 25
BTN_MUTE_LAV3			= 29
BTN_MUTE_PRGM			= 37
BTN_MUTE_CEILING		= 50
BTN_MUTE_RINGER			= 60

BTN_DISTURB			= 70 //Toggle Disturb on or Off

(***********************************************************)
(*               VARIABLE DEFINITIONS GO BELOW             *)
(***********************************************************)
DEFINE_VARIABLE

VOLATILE CHAR nBiampDevice[30] = 'Tesira VT'
VOLATILE CHAR nSipNumber[10] //= '404-385-0677'
VOLATILE CHAR nSipName[10]
VOLATILE CHAR dialPhone[20] //Dialer1
VOLATILE CHAR sLastNumber[20]
VOLATILE CHAR sIncomingCall[20]
VOLATILE INTEGER nPhoneState //On / Off
VOLATILE INTEGER nSubscribed

//Biamp Level Ranges...
VOLATILE SINTEGER nMaximum = 12 //Max...
VOLATILE SINTEGER nMinimum = -88 //Minimum...

VOLATILE INTEGER nBiampOnline
VOLATILE CHAR nBiampBuffer[2500]

VOLATILE SINTEGER nProgram_PresetMix = -10 //Set Default Level
VOLATILE SINTEGER nProgram_LevelMix //Caller + Speakers
VOLATILE INTEGER nProgram_MuteMix

VOLATILE SINTEGER nMic1_Preset = -8
VOLATILE SINTEGER nMic1_Level
VOLATILE INTEGER nMic1_Mute

VOLATILE SINTEGER nMic2_Preset = -10
VOLATILE SINTEGER nMic2_Level
VOLATILE INTEGER nMic2_Mute

VOLATILE SINTEGER nMic3_Preset = -8
VOLATILE SINTEGER nMic3_Level
VOLATILE INTEGER nMic3_Mute

VOLATILE INTEGER nCeiling_Mute //Ceiling Mics

VOLATILE DEV vdvTP_Biamp[] = 
{
    dvTP_Biamp, 
    dvTP_Biamp2
}
VOLATILE INTEGER nChnlbtns[] =
{
    //Lav 1
    21,22,23,24,
    
    //Lav 2
    25,26,27,28,
    
    //Lav 3
    29,30,31,32,
    
    //Prgm
    37,38,39,40,
       
    //Ceiling Mute
    50 //
}

    
(***********************************************************)
(*        SUBROUTINE/FUNCTION DEFINITIONS GO BELOW         *)
(***********************************************************)
(* EXAMPLE: DEFINE_FUNCTION <RETURN_TYPE> <NAME> (<PARAMETERS>) *)
(* EXAMPLE: DEFINE_CALL '<NAME>' (<PARAMETERS>) *)  
DEFINE_FUNCTION fnMuteLogic(CHAR cTag[], INTEGER cIn, CHAR cMute[MAX_SPAN])
{
    SEND_STRING dvBiamp, "cTag,' set state ',ITOA(cIn),' ',cMute,CR" 
}
DEFINE_FUNCTION fnMuteVolume(CHAR cTag[], INTEGER cIn, CHAR cValue[MAX_SPAN]) 
{
    SEND_STRING dvBiamp, "cTag,' set mute ',ITOA(cIn),' ',cValue,CR"
}
DEFINE_FUNCTION fnSetVolumeUp(CHAR cTag[], INTEGER cIn, SINTEGER cVolume)
{
    IF (cVolume < nMaximum )
    {
	SEND_STRING dvBiamp, "cTag,' set level ',ITOA(cIn),' ',ITOA(cVolume + VOL_UP),CR"
    }
}
DEFINE_FUNCTION fnSetVolumeDown(CHAR cTag[], INTEGER cIn, SINTEGER cVolume)
{
    IF (cVolume > nMinimum )
    {
	SEND_STRING dvBiamp, "cTag,' set level ',ITOA(cIn),' ',ITOA(cVolume + VOL_DN),CR"
    }
}
DEFINE_FUNCTION fnSetVolumePreset(CHAR cTag[], INTEGER cIn, SINTEGER cLevel)
{
    SEND_STRING dvBiamp, "cTag,' set level ',ITOA(cIn),' ',ITOA(cLevel),CR"
}
DEFINE_FUNCTION fnGetValues()
{
    WAIT 10 fnSetVolumePreset (TAG_MIC_MIX, ID_LAV_1, nMic1_Preset)
    WAIT 20 fnMuteVolume(TAG_MIC_MUTE, ID_LAV_1, YES_OFF)

    WAIT 30 fnSetVolumePreset (TAG_MIC_MIX, ID_LAV_2, nMic2_Preset)
    WAIT 40 fnMuteVolume(TAG_MIC_MUTE, ID_LAV_2, YES_OFF)
    
    WAIT 50 fnSetVolumePreset (TAG_MIC_MIX, ID_LAV_3, nMic3_Preset)
    WAIT 60 fnMuteVolume(TAG_MIC_MUTE, ID_LAV_3, YES_OFF)
    WAIT 70 fnSetVolumePreset(TAG_PRGM, ID_PROGRAM, nProgram_PresetMix)
    WAIT 80 fnMuteVolume(TAG_PRGM, ID_PROGRAM, YES_OFF)
    WAIT 90 fnMuteLogic(TAG_CEILING, ID_CEILING, YES_OFF)

    WAIT 110 SEND_STRING dvBiamp, "'VoIPControlStatus1 get protocols',CR"
    
    WAIT 150 fnSubscribeCalls()
    
}
DEFINE_FUNCTION fnSubscribeCalls()
{
    IF (!nSubscribed)
    {
	SEND_STRING dvBiamp, "'VoIPControlStatus1 subscribe callState Calling',CR"
	    WAIT 50 SEND_STRING dvBiamp, "'VoIPControlStatus1 get callState',CR"
    }
    ELSE
    {
	SEND_STRING dvBiamp, "'VoIPControlStatus1 get callState',CR"
    }
    
}
DEFINE_FUNCTION fnNoDisturb(CHAR cValue[MAX_SPAN])
{
    SEND_STRING dvBiamp, "'VoIPControlStatus1 set dndEnable 1 ',cValue,CR"
}
DEFINE_FUNCTION fnParseTesira()
{
	STACK_VAR CHAR cResponse[500]
	LOCAL_VAR CHAR cMsg[10]
	LOCAL_VAR CHAR cMsgId[20]
	LOCAL_VAR CHAR cCallState[10]
	LOCAL_VAR CHAR cVoipStatus[10]
	LOCAL_VAR CHAR cDbug[20]
	STACK_VAR INTEGER cID
	LOCAL_VAR CHAR cAudioLev[4]

	WHILE(FIND_STRING(nBiampBuffer,"CR,LF",1))
	{	
	    cResponse = REMOVE_STRING(nBiampBuffer,"CR,LF",1)
    
	SELECT
	{
	    //Full Caller ID Parse...
	    ACTIVE(FIND_STRING(cResponse,'! "publishToken":"Calling" "value":{"callStateInfo":[{"state":VOIP_CALL_STATE_RINGING "lineId":0 "callId":0 "action":UI_DISPLAY_STATUS "cid":"\"',1)):
	    {
		REMOVE_STRING(cResponse,'! "publishToken":"Calling" "value":{"callStateInfo":[{"state":VOIP_CALL_STATE_RINGING "lineId":0 "callId":0 "action":UI_DISPLAY_STATUS "cid":"\"',1) //Left with --> 04011103\"\"6789929391\"\"Wireless Caller\""$0D$0A
		
		REMOVE_STRING(cResponse,'\"\"',1) //Should be left with --> 6789929391\"\"Wireless Caller\""$0D$0A
	    
		cMsg = LEFT_STRING(cResponse, 10) //Just get 10 digit phone number
		REMOVE_STRING(cResponse, '\"\"',1) //Should be left with --> Wireless Caller\""$0D$0A
		
		cMsgId = cResponse 
		cMsgId = LEFT_STRING(cMsgId,LENGTH_STRING(cMsgId) -5) //Should be left with --> Wireless Caller$0D$0A
				
		SEND_COMMAND vdvTP_Biamp, "'^TXT-',ITOA(TXT_CID_NUMBER),',0,',cMsg" //Place the Number
		    SEND_COMMAND vdvTP_Biamp, "'^TXT-',ITOA(TXT_CID_NAME),',0,',cMsgId" //Place the Name...
		
		SEND_COMMAND vdvTP_Biamp, "'^PPN-_Phone'" //Show Caller PopuP
		SEND_COMMAND vdvTP_Biamp, "'^TXT-',ITOA(TXT_PHONE_STATE),',0,Incoming Call'"
	    }
	    ACTIVE(FIND_STRING(cResponse,'"channelProtoSipInfo":[{"sipUser":"',1)): //Pull SIP Number
	    {
		REMOVE_STRING(cResponse,'"channelProtoSipInfo":[{"sipUser":"',1)
		nSipNumber = LEFT_STRING(cResponse,10)
		
		REMOVE_STRING(cResponse,'" "sipDisplayName":"',1)
		nSipName = LEFT_STRING(cResponse,10)
		
		SEND_COMMAND vdvTP_Biamp, "'^TXT-',ITOA(TXT_MYPHONE),',0,',nSipNumber"
		SEND_COMMAND vdvTP_Biamp, "'^TXT-',ITOA(TXT_SIPDISAPLAY_NAME),',0,',nSipName"
	    }
	    ACTIVE(FIND_STRING(cResponse,'VoIPControlStatus1 dial 1 1 ',1)):
	    {
		REMOVE_STRING(cResponse,'VoIPControlStatus1 dial 1 1 ',1)
		cMsg = cResponse
		
		 SEND_COMMAND vdvTP_Biamp, "'^TXT-',ITOA(TXT_CID_NUMBER),',0,',cMsg" //Phone Number Block...
		 ON [nPhoneState]
		 SEND_COMMAND vdvTP_Biamp, "'^TXT-',ITOA(TXT_PHONE_STATE),',0,Connecting'"
	    }
	    ACTIVE(FIND_STRING(cResponse,'VoIPControlStatus1 end 1 1 ',1)):
	    {
		 OFF [nPhoneState]
		 SEND_COMMAND vdvTP_Biamp, "'^TXT-',ITOA(TXT_PHONE_STATE),',0,Disconnected'"
		 
		 cMsg = ''
		 cMsgId = ''
		 WAIT 50
		 {
		    SEND_COMMAND vdvTP_Biamp, "'^TXT-',ITOA(TXT_CID_NUMBER),',0,',cMsg" //Place the Number
		    SEND_COMMAND vdvTP_Biamp, "'^TXT-',ITOA(TXT_CID_NAME),',0,',cMsgId" //Place the Name...
		}
	    }
    	    ACTIVE(FIND_STRING(cResponse,'VoIPControlStatus1 answer 1 1',1)):
	    {
		SEND_COMMAND vdvTP_Biamp, "'^TXT-',ITOA(TXT_PHONE_STATE),',0,Connected'"
		ON [nPhoneState]
	    }
	    ACTIVE(FIND_STRING(cResponse,'! "publishToken":"Calling" "value":{"callStateInfo":[{"state":VOIP_CALL_STATE_',1)): //UPDATED!!!!
	    {
		REMOVE_STRING (cResponse,'! "publishToken":"Calling" "value":{"callStateInfo":[{"state":VOIP_CALL_STATE_',1)
		cCallState = LEFT_STRING(cResponse,10)
		
		IF (FIND_STRING(cCallState,'RINGING',1))
		{
		
		    SEND_COMMAND vdvTP_Biamp, "'@PPX'"
		    SEND_COMMAND vdvTP_Biamp, "'^PPN-_Phone'" //Show Caller PopuP
		    SEND_COMMAND vdvTP_Biamp, "'^TXT-',ITOA(TXT_PHONE_STATE),',0,Incoming Call'"
		}
		IF (FIND_STRING(cCallState,'DIALING',1))
		{
		    SEND_COMMAND vdvTP_Biamp, "'^TXT-',ITOA(TXT_PHONE_STATE),',0,Connecting'"
		    ON [nPhoneState]
		    WAIT 200
		    {
			SEND_STRING dvBiamp, "'VoIPControlStatus1 get callState',CR"
		    }
    		    WAIT 350
		    {
			SEND_STRING dvBiamp, "'VoIPControlStatus1 get callState',CR"
		    }
		}
		IF (FIND_STRING(cCallState,'IDLE',1))
		{
		    OFF [nPhoneState]
		    SEND_COMMAND vdvTP_Biamp, "'^TXT-',ITOA(TXT_PHONE_STATE),',0,Disconnected'"
		}
		IF (FIND_STRING(cCallState,'ACTIVE',1))
		{
		    ON [nPhoneState]
		    SEND_COMMAND vdvTP_Biamp, "'^TXT-',ITOA(TXT_PHONE_STATE),',0,Connected'"
		}
    		IF (FIND_STRING(cCallState,'ANSWER_CALL',1))
		{
		    ON [nPhoneState]
		     SEND_COMMAND vdvTP_Biamp, "'^TXT-',ITOA(TXT_PHONE_STATE),',0,Connected'"
		}
	    }
	    ACTIVE(FIND_STRING(cResponse,'+OK "value":{"callStateInfo":[]{"state":VOIP_CALL_STATE_IDLE',1)): //Get State Return...
	    {
		cDbug = cResponse
				OFF [nPhoneState]
			    SEND_COMMAND vdvTP_Biamp, "'^TXT-',ITOA(TXT_PHONE_STATE),',0,Disconnected'"
	    }
	    ACTIVE(FIND_STRING(cResponse,'+OK "value":{"callStateInfo":[]{"state":VOIP_CALL_STATE_ACTIVE',1)):
	    {
		SEND_COMMAND vdvTP_Biamp, "'^TXT-',ITOA(TXT_PHONE_STATE),',0,Connected'"
		    ON [nPhoneState]
	    }
	    ACTIVE (FIND_STRING(cResponse,'-ERR ALREADY_SUBSCRIBED',1)):
	    {
		ON [nSubscribed]
	    }
	    ACTIVE(FIND_STRING(cResponse,"TAG_PRGM,' set level ',ITOA(ID_PROGRAM)",1)):
	    {
		REMOVE_STRING(cResponse,"TAG_PRGM,' set level ',ITOA(ID_PROGRAM)",1)
		    nProgram_LevelMix = ATOI(cResponse)
			SEND_COMMAND vdvTP_Biamp, "'^TXT-',ITOA(TXT_PROGRAM),',0,',ITOA(nProgram_LevelMix + MAX_COMP),'%'"
	    }
	    ACTIVE(FIND_STRING(cResponse,"TAG_MIC_MIX,' set level '",1)):
	    {
		REMOVE_STRING(cResponse,"TAG_MIC_MIX,' set level '",1)    
		    
		cID = ATOI(LEFT_STRING(cResponse,1))
		cAudioLev = MID_STRING (cResponse,2,4)
		    
		SWITCH (cID)
		{
		    CASE ID_LAV_1 :
		    {
			nMic1_Level = ATOI(cAudioLev)
			    SEND_COMMAND vdvTP_Biamp, "'^TXT-',ITOA(TXT_LAV_1),',0,',ITOA(nMic1_Level + MAX_COMP),'%'"
		    }
		    CASE ID_LAV_2 :
		    {
			nMic2_Level = ATOI(cAudioLev)
			    SEND_COMMAND vdvTP_Biamp, "'^TXT-',ITOA(TXT_LAV_2),',0,',ITOA(nMic2_Level + MAX_COMP),'%'"
		    }
		    CASE ID_LAV_3 :
		    {
			nMic3_Level = ATOI(cAudioLev)
			    SEND_COMMAND vdvTP_Biamp, "'^TXT-',ITOA(TXT_LAV_3),',0,',ITOA(nMic3_Level + MAX_COMP),'%'"
		    }
		}
	    }
	}
    }
}

(***********************************************************)
(*                STARTUP CODE GOES BELOW                  *)
(***********************************************************)
DEFINE_START

ON [nBiampOnline]
CREATE_BUFFER dvBiamp,nBiampBuffer;


WAIT 600 //1 Minute
{
    OFF [nBiampOnline]
}

(***********************************************************)
(*                THE EVENTS GO BELOW                      *)
(***********************************************************)
DEFINE_EVENT
BUTTON_EVENT [vdvTP_Biamp, 1]
BUTTON_EVENT [vdvTP_Biamp, 2]
BUTTON_EVENT [vdvTP_Biamp, 3]
BUTTON_EVENT [vdvTP_Biamp, 4]
BUTTON_EVENT [vdvTP_Biamp, 5]
BUTTON_EVENT [vdvTP_Biamp, 6]
BUTTON_EVENT [vdvTP_Biamp, 7]
BUTTON_EVENT [vdvTP_Biamp, 8]
BUTTON_EVENT [vdvTP_Biamp, 9]
BUTTON_EVENT [vdvTP_Biamp, 10]
BUTTON_EVENT [vdvTP_Biamp, 11] 
BUTTON_EVENT [vdvTP_Biamp, 12] //Dial Pad...
{                                    
    PUSH :
    {
	STACK_VAR INTEGER nPhoneBtns
	STACK_VAR CHAR nDigit[1];
	
	nPhoneBtns = BUTTON.INPUT.CHANNEL - 1;

	TO[BUTTON.INPUT];
	
	SWITCH (nPhoneBtns)
	{
	    CASE 10:
	    {
		nDigit = '*';
		BREAK;
	    }
	    CASE 11:
	    {
		nDigit = '#';
		BREAK;
	    }
	    DEFAULT:
	    {
		nDigit = ITOA (nPhoneBtns);
		BREAK;
	    }
	}
	dialPhone = "dialPhone, nDigit"
	
	IF (nPhoneState)
	{
	    SEND_STRING dvBiamp, "'VoIPControlStatus1 dtmf  1 ',nDigit,CR"
	}
	
	SEND_COMMAND vdvTP_Biamp, "'^TXT-17,0,',dialPhone";
	
    }
}
BUTTON_EVENT [vdvTP_Biamp, 13] //BackSpace...
{
    PUSH :
    {
	IF (LENGTH_STRING(dialPhone))
	{
	    TO[BUTTON.INPUT];
	    SET_LENGTH_STRING(dialPhone,LENGTH_STRING(dialPhone) -1);
	    SEND_COMMAND vdvTP_Biamp, "'^TXT-',ITOA(TXT_NUMBER_DISPLAY),',0,',dialPhone";
	}
    }
}
BUTTON_EVENT [vdvTP_Biamp, 14] //Clear...
{
    PUSH :
    {
	dialPhone = "";
	SEND_COMMAND vdvTP_Biamp, "'^TXT-',ITOA(TXT_NUMBER_DISPLAY),',0,',dialPhone";
    }
}
BUTTON_EVENT [vdvTP_Biamp, 15] //Dial...
{
    PUSH :
    {
	IF (LENGTH_STRING (dialPhone) > 0)
	{
	    TO[BUTTON.INPUT]
	    SEND_STRING dvBiamp, "'VoIPControlStatus1 dial 1 1 ',dialPhone,CR"
	}
    }
}
BUTTON_EVENT [vdvTP_Biamp, 16] //Redial...
{
    PUSH :
    {
	TO[BUTTON.INPUT]
	SEND_STRING dvBiamp, "'VoIPControlStatus1 redial 1 1',CR"
    }
}
BUTTON_EVENT [vdvTP_Biamp, 17] //HangUp...
{
    PUSH :
    {
	TO[BUTTON.INPUT]
	SEND_STRING dvBiamp, "'VoIPControlStatus1 end 1 1 ',CR"
	
	OFF [nPhoneState]
    }
}
BUTTON_EVENT [vdvTP_Biamp, 18] //Answer...
{
    PUSH :
    {
	TO[BUTTON.INPUT]
	SEND_STRING dvBiamp, "'VoIPControlStatus1 answer 1 1 ',CR"
	ON [nPhoneState]
    }
}
BUTTON_EVENT [vdvTP_Biamp, 19] //Ignore Call
{
    PUSH :
    {
	TO[BUTTON.INPUT]
	SEND_STRING dvBiamp, "'VoIPControlStatus1 answer 1 1 ',CR"
	WAIT 10
	{
	   SEND_STRING dvBiamp, "'VoIPControlStatus1 end 1 1 ',CR"
	}
    }
}
BUTTON_EVENT [vdvTP_Biamp, nChnlbtns]
{
    PUSH :
    {
	STACK_VAR INTEGER nChnlIdx
	
	nChnlIdx = GET_LAST (nChnlbtns)
	SWITCH (nChnlIdx)
	{
	    
	    //Lav 1
	    CASE 1 :
	    {
		IF (!nMic1_Mute)
		{
		    fnMuteVolume(TAG_MIC_MUTE, ID_LAV_1, YES_ON)
		    			SEND_COMMAND vdvTP_Biamp, "'^TXT-',ITOA(TXT_LAV_1),',0,Muted'"
			    ON [nMic1_Mute]
				ON [vdvTP_Biamp, BTN_MUTE_LAV1]
		}
		ELSE
		{
		    fnMuteVolume(TAG_MIC_MUTE, ID_LAV_1, YES_OFF)
		    			SEND_COMMAND vdvTP_Biamp, "'^TXT-',ITOA(TXT_LAV_1),',0,',ITOA(nMic1_Level + MAX_COMP),'%'"
			    OFF [nMic1_Mute]
			    OFF [vdvTP_Biamp, BTN_MUTE_LAV1]
		}
	    }
	    CASE 2 : fnSetVolumeUp(TAG_MIC_MIX, ID_LAV_1, nMic1_Level)
	    CASE 3 : fnSetVolumeDown(TAG_MIC_MIX, ID_LAV_1, nMic1_Level)
	    CASE 4 : fnSetVolumePreset(TAG_MIC_MIX, ID_LAV_1, nMic1_Preset)
	    
    	    //Mic 2
	    CASE 5 :
	    {
		IF (!nMic2_Mute)
		{
		    fnMuteVolume(TAG_MIC_MUTE, ID_LAV_2, YES_ON)
			SEND_COMMAND vdvTP_Biamp, "'^TXT-',ITOA(TXT_LAV_2),',0,Muted'"
			    ON [nMic2_Mute]
				ON [vdvTP_Biamp, BTN_MUTE_LAV2]
		}
		ELSE
		{
		    fnMuteVolume(TAG_MIC_MUTE, ID_LAV_2, YES_OFF)
			SEND_COMMAND vdvTP_Biamp, "'^TXT-',ITOA(TXT_LAV_2),',0,',ITOA(nMic2_Level + MAX_COMP),'%'"
			    OFF [nMic2_Mute]
			    OFF [vdvTP_Biamp, BTN_MUTE_LAV2]
		}
	    }
	    CASE 6 : fnSetVolumeUp(TAG_MIC_MIX, ID_LAV_2, nMic2_Level)
	    CASE 7 : fnSetVolumeDown(TAG_MIC_MIX, ID_LAV_2, nMic2_Level)
	    CASE 8 : fnSetVolumePreset(TAG_MIC_MIX, ID_LAV_2, nMic2_Preset)
	    
    	    //Mic 3
	    CASE 9 :
	    {
		IF (!nMic3_Mute)
		{
		    fnMuteVolume(TAG_MIC_MUTE, ID_LAV_3, YES_ON)
			SEND_COMMAND vdvTP_Biamp, "'^TXT-',ITOA(TXT_LAV_3),',0,Muted'"
			    ON [nMic3_Mute]
				ON [vdvTP_Biamp, BTN_MUTE_LAV3]
		}
		ELSE
		{
		    fnMuteVolume(TAG_MIC_MUTE, ID_LAV_3, YES_OFF)
			SEND_COMMAND vdvTP_Biamp, "'^TXT-',ITOA(TXT_LAV_3),',0,',ITOA(nMic3_Level + MAX_COMP),'%'"
			    OFF [nMic3_Mute]
			    OFF [vdvTP_Biamp, BTN_MUTE_LAV3]
		}
	    }
	    CASE 10 : fnSetVolumeUp(TAG_MIC_MIX, ID_LAV_3, nMic3_Level)
	    CASE 11 : fnSetVolumeDown(TAG_MIC_MIX, ID_LAV_3, nMic3_Level)
	    CASE 12 : fnSetVolumePreset(TAG_MIC_MIX, ID_LAV_3, nMic3_Preset)
	    	    
    	    //Program
	    CASE 13 :
	    {
		IF (!nProgram_MuteMix)
		{
		    fnMuteVolume(TAG_PRGM, ID_PROGRAM, YES_ON)
		    			SEND_COMMAND vdvTP_Biamp, "'^TXT-',ITOA(TXT_PROGRAM),',0,Muted'"
			    ON [nProgram_MuteMix]
				ON [vdvTP_Biamp, BTN_MUTE_PRGM]
		}
		ELSE
		{
		    fnMuteVolume(TAG_PRGM, ID_PROGRAM, YES_OFF)
		    			SEND_COMMAND vdvTP_Biamp, "'^TXT-',ITOA(TXT_PROGRAM),',0,',ITOA(nProgram_LevelMix + MAX_COMP),'%'"
			    OFF [nProgram_MuteMix]
			    OFF [vdvTP_Biamp, BTN_MUTE_PRGM]
		}
	    }
	    CASE 14 : fnSetVolumeUp(TAG_PRGM, ID_PROGRAM, nProgram_LevelMix)
	    CASE 15 : fnSetVolumeDown(TAG_PRGM, ID_PROGRAM, nProgram_LevelMix)
	    CASE 16 : fnSetVolumePreset(TAG_PRGM, ID_PROGRAM, nProgram_PresetMix)
	            
	    //Ceiling
	    CASE 17 : 
	    {
		IF(!nCeiling_Mute)
		{
		    fnMuteLogic(TAG_CEILING, ID_CEILING, YES_ON)
			ON [vdvTP_Biamp, BTN_MUTE_CEILING]
			    ON [nCeiling_Mute]
		}
		ELSE
		{
		    fnMuteLogic(TAG_CEILING, ID_CEILING, YES_OFF)
		    			OFF [vdvTP_Biamp, BTN_MUTE_CEILING]
			    OFF [nCeiling_Mute]
		}
	    }
	}
    }
    HOLD [2, REPEAT] :
    {
	STACK_VAR INTEGER nChnlIdx
	
	nChnlIdx = GET_LAST (nChnlbtns)
	SWITCH (nChnlIdx)
	{
	    CASE 2 : fnSetVolumeUp(TAG_MIC_MIX, ID_LAV_1, nMic1_Level)
	    CASE 3 : fnSetVolumeDown(TAG_MIC_MIX, ID_LAV_1, nMic1_Level)
	    
    	    CASE 6 : fnSetVolumeUp(TAG_MIC_MIX, ID_LAV_2, nMic2_Level)
	    CASE 7 : fnSetVolumeDown(TAG_MIC_MIX, ID_LAV_2, nMic2_Level)
	    
    	    CASE 10 : fnSetVolumeUp(TAG_MIC_MIX, ID_LAV_3, nMic3_Level)
	    CASE 11 : fnSetVolumeDown(TAG_MIC_MIX, ID_LAV_3, nMic3_Level)
	
	    CASE 14 : fnSetVolumeUp(TAG_PRGM, ID_PROGRAM, nProgram_LevelMix)
	    CASE 15 : fnSetVolumeDown(TAG_PRGM, ID_PROGRAM, nProgram_LevelMix)
	    
	}
    }
}

DEFINE_EVENT
DATA_EVENT [dvTP_Biamp]
DATA_EVENT [dvTP_Biamp2]
{
    ONLINE:
    {
	SEND_COMMAND vdvTP_Biamp, "'^TXT-100,0,',nBiampDevice"
	SEND_COMMAND vdvTP_Biamp, "'^TXT-',ITOA(TXT_MYPHONE),',0,',nSipNumber"
	SEND_COMMAND vdvTP_Biamp, "'^TXT-',ITOA(TXT_SIPDISAPLAY_NAME),',0,',nSipName"
	
	IF (!nBiampOnline)
	{
	    fnGetValues()
	}
    }
}
DATA_EVENT [dvBiamp]
{
    ONLINE:
    {
	    SEND_COMMAND DATA.DEVICE, "'SET BAUD 115200,N,8,1 485 DISABLED'"
	    SEND_COMMAND DATA.DEVICE, "'RXON'"
	    SEND_COMMAND DATA.DEVICE, "'HSOFF'"
	    
	    WAIT 100
	    {
		   fnGetValues()
	    }
    }
    STRING:
    {
	fnParseTesira()
    }
}


DEFINE_EVENT

TIMELINE_EVENT[TL_FEEDBACK]
{    
    WAIT FIVE_MINUTES
    {
	fnSubscribeCalls()
    }

}


